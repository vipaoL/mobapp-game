plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.grgit)
}

def getVersionName = { ->
    def description = grgit.describe(tags: true, always: true)

    if (!grgit.status().clean) {
        return description + "-dirty"
    }

    return description
}

def getVersionCode = { ->
    return grgit.tag.list().size()
}

android {
    namespace 'io.github.vipaol.mobapp.game'
    compileSdk 35

    defaultConfig {
        applicationId 'io.github.vipaol.mobapp.game'
        minSdk 1
        targetSdk 35
        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += '../../src'
        main.java.srcDirs += '../../lib/mobapp-platform-android/src'
        main.java.srcDirs += '../../lib/mobapp-framework/src'
        main.java.srcDirs += '../../lib/EminiPhysicsEngine/src'

        main.java.exclude 'at/emini/physics2DDesigner/**'
        main.java.exclude 'at/emini/physics2DSimulationTests/**'
        main.java.exclude 'at/emini/physics2DUnitTests/**'
        main.java.exclude 'at/emini/physics2DVisualTest/**'
    }
}

dependencies {
}