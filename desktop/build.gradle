plugins {
    id 'java'
    alias(libs.plugins.grgit)
}

def getVersionName = { ->
    def description = grgit.describe(tags: true, always: true)

    if (!grgit.status().clean) {
        return description + "-dirty"
    }

    return description
}

group = 'com.vipaol'
version = getVersionName()

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
}

sourceSets {
    main {
        java {
            srcDirs += '../src'
            srcDirs += '../lib/mobapp-platform-desktop/src'
            srcDirs += '../lib/mobapp-framework/src'
            srcDirs += '../lib/EminiPhysicsEngine/src'

            exclude 'at/emini/physics2DDesigner/**'
            exclude 'at/emini/physics2DSimulationTests/**'
            exclude 'at/emini/physics2DUnitTests/**'
            exclude 'at/emini/physics2DVisualTest/**'
        }
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'com.vipaol.MobappGameDesktopMain'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}
